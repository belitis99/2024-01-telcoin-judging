Melodic Hazel Albatross

high

# StakingRewardsManager.sol :: topUp() The tokens to fund the staking contracts are sended to an incorrect contracts.

## Summary
**`topUp()`** is used to transferring tokens from the owner to the staking contract for funding. A issue arises as the tokens are sent to an incorrect staking contract due to the use of the for variable **`i`** instead of **`indexes`**.
## Vulnerability Detail
**`topUp()`** is used to fund staking contracts. 
```Solidity
function topUp(
        address source,
        uint256[] memory indices
    ) external onlyRole(EXECUTOR_ROLE) {
        for (uint i = 0; i < indices.length; i++) {
            // get staking contract and config
            StakingRewards staking = stakingContracts[i];
            StakingConfig memory config = stakingConfigs[staking];

            // will revert if block.timestamp <= periodFinish
            staking.setRewardsDuration(config.rewardsDuration);

            // pull tokens from owner of this contract to fund the staking contract
            rewardToken.transferFrom(
                source,
                address(staking),
                config.rewardAmount
            );

            // start periods
            staking.notifyRewardAmount(config.rewardAmount);

            emit ToppedUp(staking, config);
        }
    }
```
The issue lies in the for loop, where the variable **`i`** is used instead of **`indices[i]`** to determine the contract that should receive the funding. This leads to tokens being sent to the wrong contract.
```Solidity
 StakingRewards staking = stakingContracts[i];
```
## POC
To execute the POC, copy the provided code into the **`StakingRewardsManager.test.ts`** file.
```Solidity
describe("topUpCustom", function () {
        let newStakingConfig: StakingRewardsManager.StakingConfigStruct = {
            rewardsDuration: 60 * 60 * 24 * 7, // 1 week in seconds
            rewardAmount: 100
        };

        let indices: number[] = [2, 3];
        let tokenAmount: number = 100;

        it("amount is not transfered to the correct contracts", async function () {
            await rewardToken.connect(deployer).approve(await stakingRewardsManager.getAddress(), tokenAmount * indices.length);

            await stakingRewardsManager.createNewStakingRewardsContract(await stakingToken.getAddress(), newStakingConfig);
            await stakingRewardsManager.createNewStakingRewardsContract(await stakingToken.getAddress(), newStakingConfig);
            await stakingRewardsManager.createNewStakingRewardsContract(await stakingToken.getAddress(), newStakingConfig);
            await stakingRewardsManager.createNewStakingRewardsContract(await stakingToken.getAddress(), newStakingConfig);

            await expect(stakingRewardsManager.connect(deployer).topUp(await deployer.address, indices))
                .to.emit(stakingRewardsManager, "ToppedUp");

            for (let index of indices) {
                let stakingContract = await stakingRewardsManager.stakingContracts(index);
                expect(await rewardToken.balanceOf(stakingContract)).to.equal(0);
            }
        });
    });
```
As evident from the observation, contracts with indices 2 and 3 (contracts where we want to send the tokens) exhibit a balance of 0 due to funds being directed to contracts 0 and 1. This issue arises because the variable **`i`** in the for loop is employed instead of **`indices[i]`**.
## Impact
Tokens are sent to an incorrect contracts, resulting in a loss of funds.
## Code Snippet
https://github.com/sherlock-audit/2024-01-telcoin/blob/0954297f4fefac82d45a79c73f3a4b8eb25f10e9/telcoin-audit/contracts/telx/core/StakingRewardsManager.sol#L254-L278
## Tool used
Manual Review.
## Recommendation
To address this issue, use **`indices[i]`** instead of **`i`**.
```diff
function topUp(
        address source,
        uint256[] memory indices
    ) external onlyRole(EXECUTOR_ROLE) {
        for (uint i = 0; i < indices.length; i++) {
            // get staking contract and config
-           StakingRewards staking = stakingContracts[i];
+           StakingRewards staking = stakingContracts[indices[i]];
            StakingConfig memory config = stakingConfigs[staking];

            // will revert if block.timestamp <= periodFinish
            staking.setRewardsDuration(config.rewardsDuration);

            // pull tokens from owner of this contract to fund the staking contract
            rewardToken.transferFrom(
                source,
                address(staking),
                config.rewardAmount
            );

            // start periods
            staking.notifyRewardAmount(config.rewardAmount);

            emit ToppedUp(staking, config);
        }
    }
```