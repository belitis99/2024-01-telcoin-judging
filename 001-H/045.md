Skinny Hazelnut Gazelle

high

# Design Flaw in burn() Function May Impact Functions Using balances Array

## Summary

The `burn()` function is designed to permit the `GOVERNANCE_COUNCIL_ROLE` to execute the burning of the council member NFT. However, the burn logic contains a design flaw that may disrupt or negatively impact other functions, particularly those using the `balances` array.

In the given example, this would lead to a situation where council members are unable to execute the `claim()` function, resulting in a loss of their allocated amounts of TELCOIN.

## Vulnerability Detail

Scenario:

Initially, there are four council members: Alice, Bob, Charlie, and Dave, each holding one NFT with token IDs 0, 1, 2, and 3, respectively. The balances of each NFT are 500 Telcoin and none of them has been claimed yet.

As time passes, a new election process is completed, and Alice is no longer a council member. Therefore, the governance council needs to remove Alice from the council members by calling the `burn()` function.

```solidity=210
function burn(
    uint256 tokenId,
    address recipient
) external onlyRole(GOVERNANCE_COUNCIL_ROLE) {
    require(totalSupply() > 1, "CouncilMember: must maintain council");
    _retrieve();
    _withdrawAll(recipient, tokenId);

    uint256 balance = balances[balances.length - 1];
    balances[tokenId] = balance;
    balances.pop();
    _burn(tokenId);
}
```

https://github.com/sherlock-audit/2024-01-telcoin/blob/main/telcoin-audit/contracts/sablier/core/CouncilMember.sol#L210-L222

The function performs retrieval and distribution of Telcoin before precisely burning the NFT for Alice. Subsequently, the value of `balances[0]` is replaced with the value of the last element in the balances array, which is `balances[3]`, and then the last element is removed.

As a result, there is no longer an element 3 in the `balances` array.

```solidity=92
function claim(uint256 tokenId, uint256 amount) external {
    // Ensure the function caller is the owner of the token (council member) they're trying to claim for
    require(
        _msgSender() == ownerOf(tokenId),
        "CouncilMember: caller is not council member holding this NFT index"
    );
    // Retrieve and distribute any pending TELCOIN for all council members
    _retrieve();

    // Ensure the requested amount doesn't exceed the balance of the council member
    require(
        amount <= balances[tokenId],
        "CouncilMember: withdrawal amount is higher than balance"
    );

    // Deduct the claimed amount from the token's balance
    balances[tokenId] -= amount;
    // Safely transfer the claimed amount of TELCOIN to the function caller
    TELCOIN.safeTransfer(_msgSender(), amount);
}
```

https://github.com/sherlock-audit/2024-01-telcoin/blob/main/telcoin-audit/contracts/sablier/core/CouncilMember.sol#L92-L111

Later on, Dave, who is holding the NFT with tokenId 3, will be unable to claim his allocated Telcoin by calling the `claim()` function. This is because the `claim()` function will revert at line `103`, as there is no longer an element 3 in the `balances` array.

Additionally, the allocated Telcoin for Dave, stored in `balances[0]` during burned, cannot be retrieved either by the `claim()` or `removeFromOffice()` function since tokenId 0 has already been burned.

## Impact

Loss of allocated telcoin for council members

## Code Snippet

https://github.com/sherlock-audit/2024-01-telcoin/blob/main/telcoin-audit/contracts/sablier/core/CouncilMember.sol#L210-L222

## Tool used

Manual Review

## Recommendation

Remove the `balances` array and distribute Telcoin rewards directly by transfer to the owner of the NFT when `_retrieve()` is called.