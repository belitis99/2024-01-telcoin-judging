Brave Orchid Bat

high

# In CouncilMember.sol - balances[] array will become mismatched after burning a token

## Summary
The balances[] array that stores Council members' balances for a given `tokenId` will become mismatched after burning a token. 

This leads to people losing funds.
## Vulnerability Detail
Let's take a look at how balances is defined and the burn function:
```solidity
    // current uncliamed members balances
    uint256[] public balances;

    function burn(
        uint256 tokenId,
        address recipient
    ) external onlyRole(GOVERNANCE_COUNCIL_ROLE) {
        require(totalSupply() > 1, "CouncilMember: must maintain council");
        _retrieve();
        _withdrawAll(recipient, tokenId);
        
        //getting the last element of the balances[] array
        uint256 balance = balances[balances.length - 1];
        //rewrite the element at index `tokenId` to the last element of the array
        balances[tokenId] = balance;
        //pop the last element
        balances.pop();
        
       _burn(tokenId);
    }
```

The problem is that the function assumes that each index corresponds to a `tokenId`. 

Let's consider the following example:
- Let's assume there are 5 tokens with tokenIds from 0 to 4.
- The balances array looks like this: [10, 20, 30, 40, 50]
This means tokenId 0 has a balance of 10, tokenId 1 has a balance of 20, and so on.

- Suppose we burn tokenId 2 (which has a balance of 30).
The burn function will replace the balance at index 2 with the last element in the balances array (which is 50 from tokenId 4) and then remove the last element.
- Post-burn, the balances array looks like this: [10, 20, 50, 40]

Now, tokenId 2, which was supposed to be burned, appears to have a balance of 50, but actually, this 50 belonged to tokenId 4.

Now, the person who holds tokenId 4 with an original balance of 50 has no way to claim it. Because 50 is at index 2 and tokenId 2 is burned. If the person with tokenId 4 calls the `claim` function:
```solidity
    function claim(uint256 tokenId, uint256 amount) external {
        // Ensure the function caller is the owner of the token (council member) they're trying to claim for
        require(
            _msgSender() == ownerOf(tokenId),
            "CouncilMember: caller is not council member holding this NFT index"
        );
        // Retrieve and distribute any pending TELCOIN for all council members
        _retrieve();

        // Ensure the requested amount doesn't exceed the balance of the council member
        require(
            amount <= balances[tokenId],
            "CouncilMember: withdrawal amount is higher than balance"
        );

        // Deduct the claimed amount from the token's balance
        balances[tokenId] -= amount;
        // Safely transfer the claimed amount of TELCOIN to the function caller
        TELCOIN.safeTransfer(_msgSender(), amount);
    }
```
This requirement will fail:
```solidity
        require(
            amount <= balances[tokenId],
            "CouncilMember: withdrawal amount is higher than balance"
        );
```
As `balances[4]` currently do not even exist because after the burn, the array is of 4 elements with indexes 0, 1, 2, 3.
## Impact
Leads to a scenario where a tokenId points to an incorrect balance. Which inevitably leads to people losing funds.
## Code Snippet
https://github.com/sherlock-audit/2024-01-telcoin/blob/main/telcoin-audit/contracts/sablier/core/CouncilMember.sol#L210
## Tool used
Manual Review

## Recommendation
Instead of using an array where the index is presumed to represent the tokenId, a mapping should be utilized. A mapping from tokenId to balance (e.g., mapping(uint256 => uint256) balances) will ensure that each tokenId is directly associated with its correct balance, irrespective of the order or the burning of other tokens.